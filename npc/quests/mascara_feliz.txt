//===== Cronus Script =========================================== 
//= Quest Máscara Feliz
//===== Por: ====================================================
//= kobra_k88, Akaru
//===== Versão Atual: =========================================== 
//= 1.6
//===== Changelog: ============================================== 
//= 1.2 Traduzido grande parte [Kamper]
//= 1.3 Adaptado correção da Quest por Lupus [Mehah]
//= 1.4 Continuação da tradução [Tigow]
//= 1.5 Corrigido bug estupido (parem de usar tradutores) [Pudim]
//= - Alguem trokou next; por Próximo e close; pra fechar ¬¬
//= 1.6 Corrigido a coordenada, era de Payon Nova [Kamper]
//===============================================================

alberta.gat,113,53,3	script	Dona Risadinha::SmileAT	92,{
	mes "[Dona Risadinha]";
	mes "Oi, eu sou uma Assistente de Máscara Feliz. Como posso ajudar você?";
	next;
	menu "^0099FFMácara Feliz^000000?",M_0, "Construir uma ^0099FFMáscara Feliz^000000",M_1, "Sair",M_End;
	
	M_0:
		mes "[Dona Risadinha]";
		mes "Oh, a Mascara Feliz é um evento de âmbito nacional que esteja sempre sendo distribuido por ^0099FF Rei Tristan III^000000.";
		mes "O objetivo do evento é fazer os cidadãos de Rune Midgard pôr suas máscaras olhando felizes.";
		next;
		mes "[Dona Risadinha]";
		mes "Tendo este evento, sua majestade deseja incentivar o povo de Rune Midgard ter mais divertimento e apreciar-se um do outro!";
		mes "Como você pode ter suposto, a máscara feliz é simplesmente uma cara com um lindo rosto.";
		next;
		mes "[Dona Risadinha]";
		mes "Embora possa ser simples, ninguém podem discutir o poder de um sorriso! E se todos no reino tivessem uma....";
		mes "imagine apenas quanta alegria e a felicidade o mundo veria!";
		next;
		mes "[Dona Risadinha]";
		mes "Como dona Risadinha, é meu trabalho fazer uma máscara feliz para toda a aquelas que queira um.";
		mes "Todos os itens que eu necessito para fazer a máscara são alguns artigos pequenos, e bem faceis de achar:";
		mes "^5555FF10 Jellopy^000000";
		mes "^5555FF10 Felpas^000000";
		mes "^5555FF10 Trevos^000000";
		next;
		mes "[Dona Risadinha]";
		mes "O mundo poderia usar mais sorrisos assim que comece aqueles artigos e volte logo. Eu estarei esperando você.";
		close;
	M_1:
		mes "[Dona Risadinha]";
		if(countitem(705) < 10 || countitem(909) < 10 || countitem(914) < 10) goto sL_NoItems;
		delitem 705,10;
		delitem 909,10;
		delitem 914,10;	
		mes "Oh, ótimo! Você voltou com todos os itens necessários. Espere apenas um segundo que eu lhe darei sua Máscara Feliz.....";
		next;
		mes "~!shook!~!shook!~!clack!~!clack!~!tok!~!tok!~";
		next;
		mes "[Dona Risadinha]";
		mes "Aqui! Sua linda Máscara Feliz!";
		getitem 2278,1;
		next;
		mes "[Dona Risadinha]";
		mes "O Evento Máscara Feliz é apenas um evento dos vários caminhos que nossa majestade Tristam III está tentando fazer para uma vida melhor em Rune-Midgard.";
		next;
		mes "[Dona Risadinha]";
		mes "Com muita esperança você irá ser recompensado pela verdade todo o tempo que usar a sua Máscara Feliz.";
		close;
	
		sL_NoItems:
			mes "Oh... Que pena, você não tem todos os itens para uma Máscara Feliz. O que você precisa:";
			mes "^5555FF10 Jellopy^000000,";
			mes "^5555FF10 Felpas^000000,";
			mes "^5555FF10 Trevos^000000,";
			mes "Vou esperar aqui a sua volta.";
			close;
	M_End:
		close;
}

// Aldebaran --------------------------------------------------------------------------------
aldebaran.gat,136,135,4	duplicate(SmileAT)	Dona Risadinha#2	92
// Geffen -----------------------------------------------------------------------------------
geffen.gat,119,107,4	duplicate(SmileAT)	Dona Risadinha#3	92
// Izlude -----------------------------------------------------------------------------------
izlude.gat,129,118,4	duplicate(SmileAT)	Dona Risadinha#4	92
// Morroc -----------------------------------------------------------------------------------
morocc.gat,158,97,4	duplicate(SmileAT)	Dona Risadinha#5	92
// Payon ------------------------------------------------------------------------------------
// Payon Nova payon.gat,187,104,4	duplicate(SmileAT)	Dona Risadinha#6	92
payon.gat,83,112,4	duplicate(SmileAT)	Dona Risadinha#6	92
// Prontera ---------------------------------------------------------------------------------
prontera.gat,157,187,4	duplicate(SmileAT)	Dona Risadinha#7	92
