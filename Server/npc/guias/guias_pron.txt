//-----------------------------------------------------------
//   ____  _     _         _   _                      
//  / __ \| |   | |   /\  | | | |                     
// | |  | | | __| |  /  \ | |_| |__   ___ _ __   __ _ 
// | |  | | |/ _` | / /\ \| __| '_ \ / _ \ '_ \ / _` |
// | |__| | | (_| |/ ____ \ |_| | | |  __/ | | | (_| |
//  \____/|_|\__,_/_/    \_\__|_| |_|\___|_| |_|\__,_|
//-----------------------------------------------------------




// Centro ---------------------------------------------------------
prontera.gat,154,187,4	script	Guia::Prt_Guide	105,{
	cutin "prt_soldier",2;
	mes "[Soldado de Prontera]";
	mes "Bem Vindo a prontera a capital de Rune-Midgard.";
	next;
	mes "[Prontera Soldier]";
	mes "Nós estamos aqui para ajudar,Sinta-se avontade em falar,conosco para saber Alguma Notícia de Rune-Midgar.";
	mes "Você Gostaria de Saber das mais recentes Notícias e informações de Rune-Midgard???";
M_Menu:
	next;
	menu "Sim,Gostaria.",M_1, "Não,Obrigado.",M_End;

	M_1:
		mes "[Soldado de Prontera]";
		mes "Você quer Continuar???";
		next;
		menu "Sim,Por Favor.",sM_1a,"Não,Obrigado.",M_End;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;

		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[Soldado de Prontera]";
		mes "Por favor Selecione as Noticias.";
		next;
	sM_Menu:
		menu 	"Associação de Cavaleiros",M_1_1, "^9999F5Santuário^000000",M_1_2, "^ff0000Cavaleria De Prontera^000000",M_1_3, "Armorariá",M_1_4,
			"Loja de itens",M_1_5, "Hotel",M_1_6, "Negociador",M_1_7, "Publicação",M_1_8, "^FF8800Livraria^000000",M_1_9, "Agência JOB",M_1_10,
			"^AABBCCCastelo de Prontera^000000",M_1_11, "Salão da Cidade",M_1_12, "Oficina de Forja",M_1_13,
			"Remover Todas as Indicações.",M_1_14, "Cancelar",M_End;

		M_1_1:
			mes "[Soldado de Prontera]";
			mes "A Associação de Cavaleiros foi para Izlude, A Cidade Sátelite de Rune-Midgard.";
			viewpoint 1,237,41,1,0x2222AA;
			goto sL_Wipe;
		M_1_2:
			mes "[Soldado de Prontera]";
			mes "A igreja principal do reino de Rune-Midgard, O 'Santuário', Fica situado na parte nordeste da cidade de Prontera.";
			mes "A igreja de Prontera é o Local onde você pode-rá se tornar um Acolito,A Igreja fica a Nordeste de Prontera.";
			viewpoint 1,236,316,2,0xAAAAF5;
			goto sL_Wipe;
		M_1_3:
			mes "[Soldado de Prontera]";
			mes "A 'Cavalaria de Prontera' Trás mais Soldados para se Alistarem em Prontera.A Cavalaria Está localizada na Parte Noroeste de Prontera.";
			viewpoint 1,46,345,3,0xFF0000;
			goto sL_Wipe;
		M_1_4:
			mes "[Soldado de Prontera]";
			mes "A 'Armorariá' tem uma seleção excelente de armas e Armaduras.";
			viewpoint 1,175,220,4,0xFF00FF;
			goto sL_Wipe;
		M_1_5:
			mes "[Soldado de Prontera]";
			mes "Você pode Encontrar Muitas Coisas Interessantes na Loja de Itens.";
			viewpoint 1,134,221,5,0xFF00FF;
			goto sL_Wipe;
		M_1_6:
			mes "[Soldado de Prontera]";
			mes "Há 2 Hotéis situados nos lados,Ocidentais e Orientais da cidade de Prontera.";
			viewpoint 1,204,195,6,0x00DD55;
			viewpoint 1,107,212,7,0x00DD55;
			goto sL_Wipe;
		M_1_7:
			mes "[Soldado de Prontera]";
			mes "O 'Negociador' Fica a Sudeste de Prontera,Logo Atrás da Fonte.";
			viewpoint 1,179,184,8,0x7901AD;
			goto sL_Wipe;
		M_1_8:
			mes "[Soldado de Prontera]";
			mes "A 'publicação' é o ponto forte de Prontera,quem sabe você consiga vender seu Item nesta cidade.";
			viewpoint 1,208,154,9,0xAA0077;
			goto sL_Wipe;
		M_1_9:
			mes "[Soldado de Prontera]";
			mes "A 'Livraria' Abriga uma grande quantidade de Informações,vasta em Informações Sobre Os Monstros de Rune-Midgard.A Biblioteca fica ao Norte de Rune-Midgard.";
			viewpoint 1,120,267,10,0xFF8800;
			viewpoint 1,192,267,11,0xFF8800;
			goto sL_Wipe;
		M_1_10:
			mes "[Soldado de Prontera]";
			mes "A 'Agência JOB' está sudoeste,Próximo a Fonte.";
			viewpoint 1,133,183,12,0x4444FF;
			goto sL_Wipe;
		M_1_11:
			mes "[Soldado de Prontera]";
			mes "Os 'Castelos de Prontera' Se Localizam ao Norte da Cidade de Prontera";
			mes "No outro lado do castelo está o campo norte de Prontera.";
			viewpoint 1,156,360,13,0xAABBCC;
			goto sL_Wipe;
		M_1_12:
			mes "[Soldado de Prontera]";
			mes " O 'Salão da Cidade' fica situada no canto do sudoeste da cidade.";
			viewpoint 1,75,91,14,0xAAEE00;
			goto sL_Wipe;
	
		M_1_13:
			mes "[Soldado de Prontera]";
			mes " A 'Oficina de Forja' é o lugar onde você pode refinar suas Armas e Armaduras.";
			viewpoint 1,180,183,15,0xFF4477;
			goto sL_Wipe;
		M_1_14:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;	// avoids a double 'next' when 'wipe indications' is chosen
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,237,41,1,0x2222AA;
			viewpoint 2,236,316,2,0xAAAAF5;
			viewpoint 2,46,345,3,0xFF0000;
			viewpoint 2,175,220,4,0xFF00FF;
			viewpoint 2,134,221,5,0xFF00FF;
			viewpoint 2,204,195,6,0x00DD55;
			viewpoint 2,107,212,7,0x00DD55;
			viewpoint 2,179,184,8,0x7901AD;
			viewpoint 2,208,154,9,0xAA0077;
			viewpoint 2,120,267,10,0xFF8800;
			viewpoint 2,192,267,11,0xFF8800;
			viewpoint 2,133,183,12,0x4444FF;
			viewpoint 2,156,360,13,0xAABBCC;
			viewpoint 2,75,91,14,0xAAEE00;
			viewpoint 2,180,183,15,0xFF4477;
			if (@COMPASS_CHECK == 0) goto sM_Menu;
			mes "[Soldado de Prontera]";
			mes "Todas as Indicações foram removidas.";
			cutin "prt_soldier",255;
			close;

	M_End:
		mes "[Soldado de Prontera]";
		mes "Tenha um Bom Dia.";
		cutin "prt_soldier",255;
		close;
}

prontera.gat,282,208,2	duplicate(Prt_Guide)	Guia#2	105
// Oeste --------------------------------------------------
prontera.gat,29,200,6	duplicate(Prt_Guide)	Guia#3	105
// Sul -----------------------------------------------------
prontera.gat,160,29,0	duplicate(Prt_Guide)	Guia#4	105
// Norte -----------------------------------------------
prontera.gat,151,330,4	duplicate(Prt_Guide)	Guia#5	105

// Guarda -----------------------------------------------------
prontera.gat,160,330,4	script	Guarda#1::Prt_Guard	105,{
	mes "[Guarda]";
	mes "Bem Vindo a prontera.";
	close;
}

prontera.gat,229,104,5	duplicate(Prt_Guard)	Guarda#guarda_prt2	105
prontera.gat,223,98,8	duplicate(Prt_Guard)	Guarda#guarda_prt3	105
prontera.gat,52,344,5	duplicate(Prt_Guard)	Guarda#guarda_prt4	105
prontera.gat,47,339,5	duplicate(Prt_Guard)	Guarda#guarda_prt5	105
//---------------------------------------------------------------------------------------- Prontera Maze ----------------------------------------------------------------------------------\\
prt_maze02.gat,100,69,4	script	Guarda#guarda_maze1	105,{
	mes "[Soldado]";
	mes "Ei.Você, acho melhor você não vir Para esse lugar.";
	mes "Se Você Quiser,o Problema é seu.";
	mes "Só te digo uma coisa:";
	mes "Tome,Cuidado com os Monstros.";
	close;
}
prt_maze02.gat,110,69,4	script	Guarda#guarda_maze2	105,{
	mes "[Soldier]";
	mes "O nosso último amigo entrou neste lugar, e não voltou tão rapido. Não me escutou e não foi Bem-Sucedido ";
	mes "Então Me escute:";
	mes "Não entre neste lugar!!!";
	close;
}

