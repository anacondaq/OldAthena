//-----------------------------------------------------------
//   ____  _     _         _   _                      
//  / __ \| |   | |   /\  | | | |                     
// | |  | | | __| |  /  \ | |_| |__   ___ _ __   __ _ 
// | |  | | |/ _` | / /\ \| __| '_ \ / _ \ '_ \ / _` |
// | |__| | | (_| |/ ____ \ |_| | | |  __/ | | | (_| |
//  \____/|_|\__,_/_/    \_\__|_| |_|\___|_| |_|\__,_|
//-----------------------------------------------------------


// -- Expert Mage -- 
geffen_in.gat,164,124,4	script	Maga Guardiã	123,{
	callfunc "F_ToHigh",26,"High Mage",33,"High Wizard",157,0,0,0,"Maga Guardiã";
	if(BaseJob==Job_Novice && job_magician_q >= 1) goto L_BeMage;

	mes "[Maga Guardiã]";
	mes "Oi oi!  Como posso ajuda-lo?";
M_Menu:
	next;
	menu "Sou um apaixonado por magias, quero ser um Mago!",M_0,"Fale-me os Requirimentos.",M_1,"Nada, Obrigado.",M_End;

	M_0:
		mes "[Maga Guardiã]";
		mes "Você quer ser um Mago? Hmmm.....";
		next;
		mes "[Maga Guardiã]";
		callfunc "Mag_check";
		mes "A única coisa que precisa é passar no teste dos Magos de Geffen. Deixe-me ver uma coisinha...";
		next;
		set job_magician_q, rand(1,4);

		L_Explain:
		mes "[Maga Guardiã]";
		if(job_magician_q == 1) mes "Seu teste é fazer a ^0000ff'Solução Mista No.1'^000000 e trazer de volta para mim.";
		if(job_magician_q == 2) mes "Seu teste é fazer a ^00aa00'Solução Mista No.2'^000000 e trazer de volta para mim.";
		if(job_magician_q == 3) mes "Seu teste é fazer a ^ff0000'Solução Mista No.3'^000000 e trazer de volta para mim.";
		if(job_magician_q == 4) mes "Seu teste é fazer a ^ffbb00'Solução Mista No.4'^000000 e trazer de volta para mim.";

		next;
		if(countitem(1092) == 0) getitem 1092,1;//Items: Empty_Test_Tube,
		mes "[Maga Guardiã]";
		mes "Você pode adquirir os ingredientes necessários no ^5533FF'Livro de Guia'^000000 nesta sala.";
		mes "Recorrem a isto para ver a lista dos ingredientes necessários para a solução.";
		next;
		mes "[Maga Guardiã]";
		mes "Quando você estiver pronto para misturar isto, use a ^ff0000'Máquina Misturadora'^000000 no centro para misturar a solução.";
		next;
		mes "[Maga Guardiã]";
		mes "Boa Sorte!  Eu estarei esperando.";
		close;

	M_1:
		mes "[Maga Guardiã]";
		mes "Antes de tudo, tem de conseguir nível 10 de classe de aprendiz e todas as habilidades básicas.";
		mes "Depois, você tem que passar no teste dos magos.";
		goto M_Menu;
	M_End:
		mes "[Maga Guardiã]";
		mes "Ok.  Tchau!.";
		close;

L_BeMage:
	mes "[Maga Guardiã]";
	mes "Oh, você voltou.  Deixa eu ver....";
	next;
	mes "[Maga Guardiã]";
	if(countitem(1090) >= 1) goto L_Retry;//Items: Unknown_Test_Tube,
	if((countitem(1071)<1) && (countitem(1085)<1) && (countitem(1086)<1) && (countitem(1087)<1)) goto L_NoSol;//Items: Unknown_Test_Tube, Unknown_Test_Tube, Unknown_Test_Tube, Unknown_Test_Tube,

	if(SkillPoint > 0) mes "Você precisa gastar todos os seus pontos de habilidade antes que eu possa lhe tornar um Mago.";
	if(SkillPoint > 0) close;

	mes ".....As cores não estão muito certas.... percebe-se uma pequena diferença..... hmmm......";
	next;
	mes "[Maga Guardiã]";
	mes "Bem, não está perfeito mas parece que você trabalhou muito duro nisto assim...... certo! Eu o deixarei passar!  Você esta agora ofcialmente qualificado para se tornar um Mago!";
	emotion 21;
	next;
	mes "[Maga Guardiã]";
	mes "Abrakadabra, transforme esse jovem aprendiz em um ^00aa00MAGO^000000!!";
	next;
	callfunc "Job_Change",Job_Mage;

	set Zeny,Zeny+500;
	if(job_magician_q == 1) delitem 1071,1;//Items: Unknown_Test_Tube,
	if(job_magician_q == 2) delitem 1085,1;//Items: Unknown_Test_Tube,
	if(job_magician_q == 3) delitem 1086,1;//Items: Unknown_Test_Tube,
	if(job_magician_q == 4) delitem 1087,1;//Items: Unknown_Test_Tube,

	callfunc "F_ClearJobVar";		// clears all job variables for the current player
	mes "[Maga Guardiã]";
	mes "'Bem-vindo a Meu Mundo' ~ hehe .  Eu sempre quis dizer isso.  De qualquer maneira, parabéns!. Você é agora um de nós, então que  sejamos os amigos!";
	emotion 18;
	next;
	mes "[Maga Guardiã]";
	mes "Booa sorte com os itens que você terá que pegar!";
	close;

	L_Retry:
		mes "Eu sinto muito mas a solução que você tem não é boa.  Eu acho que você não passou no teste.  Entretanto você pode tentar novamente.";
		mes "Deixe-me explicar novamente o teste para você";
		emotion 4;
		next;
		delitem 1090,1;//Items: Unknown_Test_Tube,
		goto L_Explain;

	L_NoSol:
		mes "Onde a solução é que eu pedi?  Eu sinto muito mas eu não posso conferir a solução se você não tive-la. Deixe-me lembrar de que solução você precisa.";
		emotion 1;
		next;
		goto L_Explain;

}

// -- Solutions Guide Book --
geffen_in.gat,177,112,1	script	Livro de Guia	111,{
	mes "[Livro de Guia de Soluções]";
	mes "Eu pertenço a Academia de Mágica de Geffen.  Por favor use-me com cuidado.";
	next;
	menu "Solução Nº. 1.",Sol1,"Solução Nº. 2.",Sol2,"Solução Nº. 3.",Sol3,"Solução Nº. 4.",Sol4,"Sair.",Cancel;

	Sol1:
		mes "[Teste de Mago - Solução No. 1]";
		mes "* Lista de Ingredientes *";
		mes " - 2 Jellopy";
		mes " - 3 Felpas";
		mes " - 1 Leite";
		next;
		mes "[Teste de Mago - Solução No. 1]";
		mes "* Solvente *";
		mes " - Solução de Payon ";
		mes "   Onde achar: em uma pequna fonte na";
		mes "   Vila dos Arqueiros, Payon.";
		next;
		mes "[Teste de Mago - Solução No. 1]";
		mes "* Número Mágico *";
		mes " - 8472";
		next;
		mes "[Teste de Mago - Solução No. 1]";
		mes "* Pedra de Catalisador *";
		mes " - Gema Amarela";
		mes "   (Maquina de Mistura irá providenciar";
		mes "   este item.)";
		close;
	Sol2:
		mes "[Teste de Mago - Solução No. 2]";
		mes "* Lista de Ingredientes *";
		mes " - 3 Jellopy";
		mes " - 1 Felpa";
		mes " - 1 Leite";
		next;
		mes "[Teste de Mago - Solução No. 2]";
		mes "* Solvente *";
		mes " - Nenhum.";
		next;
		mes "[Teste de Mago - Solução No. 2]";
		mes "* Número Mágico *";
		mes " - 3735";
		next;
		mes "[Teste de Mago - Solução No. 2]";
		mes "* Pedra de Catalisador *";
		mes " - Gema Vermelha";
		mes "   (Maquina de Mistura irá providenciar";
		mes "   este item.)";
		close;
	Sol3:
		mes "[Teste de Mago - Solução No. 3]";
		mes "* Lista de Ingredientes *";
		mes " - 6 Jellopy";
		mes " - 1 Felpa";
		next;
		mes "[Teste de Mago - Solução No. 3]";
		mes "* Solvent Agent *";
		mes " - Solução de Payon ";
		mes "   Onde achar: em uma pequna fonte na";
		mes "   Vila dos Arqueiros, Payon.";
		next;
		mes "[Teste de Mago - Solução No. 3]";
		mes "* Número Mágico *";
		mes " - 2750";
		next;
		mes "[Teste de Mago - Solução No. 3]";
		mes "* Pedra de Catalisador *";
		mes " - Gema Azul";
		mes "   (Maquina de Mistura irá providenciar";
		mes "   este item.)";
		close;
	Sol4:
		mes "[Teste de Mago - Solução No. 4]";
		mes "* Lista de Ingredientes **";
		mes " - 2 Jellopy";
		mes " - 3 Felpas";
		next;
		mes "[Teste de Mago - Solução No. 4]";
		mes "* Solvent Agent *";
		mes " - Solução de Morroc ";
		mes "   Onde achar: em uma pequena fonte";
		mes "   Perto da entrada da Pirâmede";
		mes "   em Morroc.";
		next;
		mes "[Teste de Mago - Solução No. 4]";
		mes "* Número Mágico *";
		mes " - 5429";
		next;
		mes "[Teste de Mago - Solução No. 4]";
		mes "* Pedra de Catalisador *";
		mes " - 1 Diamante";
		mes "   (Maquina de Mistura irá providenciar";
		mes "   este item.)";
		close;
	Cancel:
		close;
}

// -- Mixing Machine --
geffen_in.gat,164,112,1	script	Maquina de Mistura	111,{
	mes "[Maquina de Mistura]";
	mes "Esta é a Máquina de Mistura. Sem igual, projetada só para fazer soluções mágicas.  Isto é propriedade da Academia Mágica de Geffen.";
	next;
	mes "[Maquina de Mistura]";
	mes "Você gostaria de usar esta máquina?";
	next;
	menu "Sim.",M_Start, "Não.",M_End;

	M_Start:
		mes "[Maquina de Mistura]";
		mes "Por favor especifique os ingredientes que você estará usando.";
		next;
		sM_Menu:
		menu "Jellopy",sM_0, "Felpa",sM_1, "Leite",sM_2, "Ir para o próximo passo",sM_3;

		sM_0:
			input @JELLOPY;
			if (@JELLOPY<0 || @JELLOPY>100) set @JELLOPY,0; //set 0 if wrong input
			goto sM_Menu;
		sM_1:
			input @FLUFF;
			if (@FLUFF<0 || @FLUFF>100) set @FLUFF,0; //set 0 if wrong input
			goto sM_Menu;
		sM_2:
			input @MILK;
			if (@MILK<0 || @MILK>100) set @MILK,0; //set 0 if wrong input
			goto sM_Menu;
		sM_3:
			mes "[Maquina de Mistura]";
			mes "Por favor especifique uma solução.";
			next;
			menu "Solução de Payon.",ssM_0, "Solução de Morroc",ssM_1, "Nenhuma solução.",ssM_2;

			ssM_0:
				set @SOLVENT, 1089;
				set @SOLVENT$, "Payon";
				goto L_Cont;
			ssM_1:
				set @SOLVENT, 1088;
				set @SOLVENT$, "Morroc";
				goto L_Cont;
			ssM_2:
				set @SOLVENT, 0;
				set @SOLVENT$, "Lugar Nenhum";

			L_Cont:
			mes "[Maquina de Mistura]";
			mes "Aqui esta sua atual lista de ingredientes:";
			mes "^4444FF"+@JELLOPY+"^000000 Jellopy,";
			mes "^4444FF"+@FLUFF+"^000000 Fluff,";
			mes "^4444FF"+@MILK+"^000000 Milk,";
			mes "e Solução de ^4444FF"+@SOLVENT$+"^000000.";
			mes "esta correto?";
			next;
			menu "Sim.",-, "Não.",M_Start;

			mes "[Maquina de Mistura]";
			mes "Por favor ponha todos os artigos no recipiente.  Agora entre no número mágico.";
			next;

			L_input:
			input @input;
			mes "[Maquina de Mistura]";
			mes "Você colocou o número ^5555FF"+@input+".  Está correto?";
			next;
			menu "Sim.",-, "Não.",L_input;

			mes "[Maquina de Mistura]";
			mes "Por o último, escolha uma pedra de catalisador.";
			next;
			menu "Gema Amarela.",ssM_Yell, "Gema Vermelha.",ssM_Red, "Gema Azul.",ssM_Blue, "1 Diamante.",ssM_Diamd;

			ssM_Yell:
				if(countitem(909)<@JELLOPY || countitem(914)<@FLUFF || countitem(519)<@MILK) goto sL_NotEnuf;//Items: Jellopy, Fluff, Milk,
				if(@SOLVENT$!=("No") && countitem(@SOLVENT)<1) goto sL_NotEnuf;
				mes "[Maquina de Mistura]";
				mes "Tudo no lugar.  Iniciando o processo de misturanção.  Por favor espere.";
				next;
				mes "[Maquina de Mistura]";
				mes "~rumble~rumble~rumble~";
				next;
				mes "[Maquina de Mistura]";
				if(job_magician_q != 1 || @input != 8472 || @JELLOPY!=2 || @FLUFF!=3 || @MILK!=1 || @SOLVENT!=1089) goto sL_FAIL;
				delitem 909,2;//Items: Jellopy,
				delitem 914,3;//Items: Fluff,
				delitem 519,1;//Items: Milk,
				delitem 1089,1;//Items: Payon_Solution,
				delitem 1092,1;//Items: Empty_Test_Tube,
				mes "Mistura completa.";
				getitem 1071,1;//Items: Unknown_Test_Tube,
				close;

			ssM_Red:
				if(countitem(909)<@JELLOPY || countitem(914)<@FLUFF || countitem(519)<@MILK) goto sL_NotEnuf;//Items: Jellopy, Fluff, Milk,
				mes "[Maquina de Mistura]";
				mes "Tudo no lugar.  Iniciando o processo de misturanção.  Por favor espere.";
				next;
				mes "[Maquina de Mistura]";
				mes "~rumble~rumble~rumble~";
				next;
				mes "[Maquina de Mistura]";
				if(job_magician_q != 2 || @input != 3735 || @JELLOPY!=3 || @FLUFF!=1 || @MILK!=1 || @SOLVENT!=0) goto sL_FAIL;
				delitem 909,3;//Items: Jellopy,
				delitem 914,1;//Items: Fluff,
				delitem 519,1;//Items: Milk,
				delitem 1092,1;//Items: Empty_Test_Tube,
				mes "Mistura completa.";
				getitem 1085,1;//Items: Unknown_Test_Tube,
				close;

			ssM_Blue:
				if(countitem(909)<@JELLOPY || countitem(914)<@FLUFF) goto sL_NotEnuf;//Items: Jellopy, Fluff,
				if(@SOLVENT$!=("No") && countitem(@SOLVENT)<1) goto sL_NotEnuf;
				mes "[Maquina de Mistura]";
				mes "Tudo no lugar.  Iniciando o processo de misturanção.  Por favor espere.";
				next;
				mes "[Maquina de Mistura]";
				mes "~rumble~rumble~rumble~";
				next;
				mes "[Mixing Machine]";
				if(job_magician_q != 3 || @input != 2750 || @JELLOPY!=6 || @FLUFF!=1 || @MILK!=0 || @SOLVENT!=1089) goto sL_FAIL;
				delitem 909,6;//Items: Jellopy,
				delitem 914,1;//Items: Fluff,
				delitem 1089,1;//Items: Payon_Solution,
				delitem 1092,1;//Items: Empty_Test_Tube,
				mes "Mistura completa.";
				getitem 1086,1;//Items: Unknown_Test_Tube,
				close;

			ssM_Diamd:
				if(countitem(909)<@JELLOPY || countitem(914)<@FLUFF) goto sL_NotEnuf;//Items: Jellopy, Fluff,
				if(@SOLVENT$!=("No") && countitem(@SOLVENT)<1) goto sL_NotEnuf;
				mes "[Maquina de Mistura]";
				mes "Tudo no lugar.  Iniciando o processo de misturanção.  Por favor espere.";
				next;
				mes "[Maquina de Mistura]";
				mes "~rumble~rumble~rumble~";
				next;
				mes "[Maquina de Mistura]";
				if(job_magician_q != 4 || @input != 5429 || @JELLOPY!=2 || @FLUFF!=3 || @MILK!=0 || @SOLVENT!=1088) goto sL_FAIL;
				delitem 909,2;//Items: Jellopy,
				delitem 914,3;//Items: Fluff,
				delitem 1088,1;//Items: Morroc_Solution,
				delitem 1092,1;//Items: Empty_Test_Tube,
				mes "Mistura completa.";
				getitem 1087,1;//Items: Unknown_Test_Tube,
				close;

			sL_FAIL:
				delitem 909, @JELLOPY;//Items: Jellopy,
				delitem 914, @FLUFF;//Items: Fluff,
				delitem 519, @MILK;//Items: Milk,
				delitem @SOLVENT,1;
				mes "Mistura completa.";
				getitem 1090,1;//Items: Unknown_Test_Tube,
				close;

			sL_NotEnuf:
				mes "[Maquina de Mistura]";
				mes "Você não tem bastante ingredientes para misturar em uma solução.";
				close;
	M_End:
		close;

}


// -- Morroc Solution --
moc_ruins.gat,91,150,5	script	Ponka-Hontas	93,{
	mes "[Ponka-Hontas]";
	mes "Você quer a Solução? Então me dê 50 Zeny e pelo menos 1 Tubo de teste vazio.";
	next;
	menu "Tudo bem, vou dar",L0,"Nah, esqueça.",L1;

	L0:
		if(countitem(1092)<1) goto L0NoT;//Items: Empty_Test_Tube,
		if(Zeny<50) goto L0NoZ;
		mes "[Ponka-Hontas]";
		mes "Ok, você tem dinheiro e um tubo de testes vazio, isso é bom!";
		delitem 1092,1;//Items: Empty_Test_Tube,
		set Zeny,Zeny-50;
		next;
		mes "[Ponka-Hontas]";
		getitem 1088,1;//Items: Morroc_Solution,
		mes "Obrigado. E acho que você será um grande mago, és muito generoso";
		mes "Eu espero o ver novamente aqui";
		emotion 21;
		close;

		L0NoT:
			mes "[Ponka-Hontas]";
			mes "Você não pode continuar sem uma garrafa, você está seguro você quer se tornar um mago?";
			mes "Traga um Tubo de testes vazio";
			close;

		L0NoZ:
			mes "[Ponka-Hontas]";
			mes "Pfff, Você é muito pobre para para comprar nossa Solução nobre!";
			mes "Traga 50 Zeny. Vá para o mercado e venda algo, não será muito duro, eu espero.";
			close;
	L1:
		mes "[Ponka-Hontas]";
		mes "Ok, até mais.";
		close;
}


// -- Payon Solution --
pay_arche.gat,122,100,5	script	Dollshoi	88,{
	mes "[Dollshoi]";
	mes "Você quer a Solução? Então me dê 50 Zeny e pelo menos 1 Tubo de teste vazio.";
	next;
	menu "Tudo bem, vou dar",L0,"Nah, esqueça.",L1;

	L0:
		if(countitem(1092)<1) goto L0NoT;//Items: Empty_Test_Tube,
		if(Zeny<50) goto L0NoZ;
		mes "[Dollshoi]";
		mes "Ok, você tem dinheiro e um tubo de testes vazio, isso é bom!";
		delitem 1092,1;//Items: Empty_Test_Tube,
		set Zeny,Zeny-50;
		next;
		mes "[Dollshoi]";
		getitem 1089,1;//Items: Payon_Solution,
		mes "Obrigado. E acho que você será um grande mago, és muito generoso";
		mes "Eu espero o ver novamente aqui";
		emotion 21;
		close;

		L0NoT:
			mes "[Dollshoi]";
			mes "Você não pode continuar sem uma garrafa, você está seguro você quer se tornar um mago?";
			mes "Traga um Tubo de testes vazio";
			close;

		L0NoZ:
			mes "[Dollshoi]";
			mes "Pfff, Você é muito pobre para para comprar nossa Solução nobre!";
			mes "Traga 50 Zeny. Vá para o mercado e venda algo, não será muito duro, eu espero.";
			close;
	L1:
		mes "[Dollshoi]";
		close;
}

